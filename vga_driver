module vga_driver (
    input wire clk, // 25 MHz system clock
    input wire rst,       // Reset input
    output reg hsync,     // Horizontal sync output
    output reg vsync,     // Vertical sync output
    output reg [9:0] h_count,
    output reg [9:0] v_count
);

// VGA parameters for 640x480 resolution
parameter H_ACTIVE = 640;
parameter H_FRONT_PORCH = 16;
parameter H_SYNC_PULSE = 96;
parameter H_BACK_PORCH = 48;
parameter H_TOTAL = 800;

parameter V_ACTIVE = 480;
parameter V_FRONT_PORCH = 10;
parameter V_SYNC_PULSE = 2;
parameter V_BACK_PORCH = 33;
parameter V_TOTAL = 525;

// Updating horizontal and vertical counters
always @(posedge clk or posedge rst) begin
    if (rst) begin
        h_count <= 0;
        v_count <= 0;
    end else begin
        if (h_count == H_TOTAL - 1) begin
            h_count <= 0;
            if (v_count == V_TOTAL - 1)
                v_count <= 0;
            else
                v_count <= v_count + 1;
        end else
            h_count <= h_count + 1;
    end
end

// Generating hsync and vsync signals
always @(*) begin
    hsync = ~((h_count >= (H_ACTIVE + H_FRONT_PORCH)) && 
              (h_count < (H_ACTIVE + H_FRONT_PORCH + H_SYNC_PULSE)));
    vsync = ~((v_count >= (V_ACTIVE + V_FRONT_PORCH)) && 
              (v_count < (V_ACTIVE + V_FRONT_PORCH + V_SYNC_PULSE)));
end

endmodule
